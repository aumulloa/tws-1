%{
#include <stdio.h>
#include <string.h>
#include <assert.h>
#include <Tokenizer.h>
#include "y.tab.h"

static int line = 1;
static int column = 1;
int rule(int token);
int node(int token);
void yyerror(char* message);
int debug_tokenizer = 0;

%}
%x COMM1
COMM2   #.*\n
CAPCONST [A-Z][_a-zA-Z0-9]*
IDENT	[_a-zA-Z][_a-zA-Z0-9]*
INT	[0-9]+
WHITE   [ \t\v\f]*
LIT     '[^\']+'
STR     \"[^\"]*\"
%%

{WHITE}    	{ column += yyleng; }
\n         	{ column = 1; line++; }
"program"    	{ return rule(PROGRAM); }
"var" 		{ return rule(VAR); }
"const"		{ return rule(CONST); }
"type"		{ return rule(TYPE); }

"begin" 	{ return rule(BEGINX); }
"end" 		{ return rule(END); }

":=:"		{ return rule(EQSWAP); }

":=" 		{ return rule(ASSIGNMENT); }
"output" 	{ return rule(OUTPUT); }
"if" 		{ return rule(IF); }
"then" 		{ return rule(THEN); }
"else" 		{ return rule(ELSE); }
"while" 	{ return rule(WHILE); }
"do" 		{ return rule(DO); }
"repeat" 	{ return rule(REPEAT); }
"until" 	{ return rule(UNTIL); }

"loop"		{ return rule(LOOP); }
"pool" 		{ return rule(POOL); }

"exit"  	{ return rule(EXIT); }


"for"		{ return rule(FOR); }
"to"		{ return rule(TO); }
"downto"	{ return rule(DOWNTO); }

"of"		{ return rule(OF); }
"case"		{ return rule(CASE); }
".."		{ return rule(RANGE); }
"otherwise"	{ return rule(OTHERWISE); }

">"		{ return rule(GT); }
"<"		{ return rule(LT); }
">=" 		{ return rule(GTE); }
"<>" 		{ return rule(NE); }
"="		{ return rule(EQ); }
"<=" 		{ return rule(LTE); }
"or" 		{ return rule(OR); }
"mod" 		{ return rule(MOD); }
"and" 		{ return rule(AND); }
"**"		{ return rule(POW); }
"*"             { return rule(yytext[0]); }
"/"             { return rule(yytext[0]); }
"not"           { return rule(NOT); }

"read" 		{ return rule(READ); }
"eof" 		{ return rule(Eof); }

"pred" 		{ return rule(PRED); }
"succ" 		{ return rule(SUCC); }
"chr" 		{ return rule(CHR); }
"ord" 		{ return rule(ORD); }

{INT}		{ return node(INTEGER_NUM); }
{LIT}		{ return node1(CHARACTER_ALPHA); }

{STR}		{ return node(STR_ALPHA); }

{CAPCONST}	{ return node(CAPIDENTIFIER); }

{IDENT}		{ return node(IDENTIFIER); }



"{"		{ column += yyleng; BEGIN( COMM1 ); }
<COMM1>[^}\n]+  { column += yyleng; }
<COMM1>"}"	{ column += yyleng; BEGIN( INITIAL ); }
<COMM1>\n	{ column = 1; line++; }
{COMM2}         { column = 1; line++; }
":"             { return rule(yytext[0]); }
";"             { return rule(yytext[0]); }
"."             { return rule(yytext[0]); }
","             { return rule(yytext[0]); }
"("             { return rule(yytext[0]); }
")"             { return rule(yytext[0]); }
"+"             { return rule(yytext[0]); }
"-"             { return rule(yytext[0]); }
.               { yyerror("unrecognized char");
                  printf("-->%s<--\n",yytext);
                  column++; }  
%%

int rule(int token)
{
    if (debug_tokenizer) {
	printf("string:%s, token: %d, line: %d, column: %d\n",
	       yytext,token,line,column);
    }

    yylval.info.line = line;
    yylval.info.column = column;
    column += yyleng;

    yylval.info.string = malloc(yyleng+1);
    assert(yylval.info.string);
    strcpy(yylval.info.string, yytext); 

    yylval.info.makenode = 0;

    return token;
}

int node(int token)
{
    int tok = rule(token);
    yylval.info.makenode = 1;
    return tok;
}

void yyerror(char* message)
{
    printf("****** %s @line %d, column %d\n",
	   message,line,column);
}

int rule1(int token)
{
    if (debug_tokenizer) {
	printf("string:%s, token: %d, line: %d, column: %d\n",
	       yytext,token,line,column);
    }

    yylval.info.line = line;
    yylval.info.column = column;
    column += yyleng;

    yylval.info.string = malloc(yyleng+1-2);
    assert(yylval.info.string);
    yytext++;
    yytext[1]= '\0';
    strcpy(yylval.info.string, yytext); 

    yylval.info.makenode = 0;

    return token;
}

int node1(int token)
{
    int tok = rule1(token);
    yylval.info.makenode = 1;
    return tok;
}